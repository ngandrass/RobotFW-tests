*** Settings ***
Library             PeriphUTimer  port=%{PORT}  baudrate=%{BAUD}  timeout=${%{HIL_CMD_TIMEOUT}}  connect_wait=${%{HIL_CONNECT_WAIT}}

Resource            api_shell.keywords.txt
Resource            philip.keywords.txt

*** Keywords ***
Measure Timer Set Delay
    [Documentation]             Evaluates delay between timer_set and entering
    ...                         the timer ISR callback (measured by PHiLIP)
    [Arguments]                 ${ticks}
    PHILIP Trace GPIO 0
    API Call Should Succeed     Timer Debug Pin  dev=0  port=%{HIL_DUT_GPIO0_PORT}  pin=%{HIL_DUT_GPIO0_PIN}
    API Call Should Succeed     Timer Init  freq=%{HIL_PERIPH_TIMER_HZ}
    API Call Should Succeed     Timer Start
    API Call Should Succeed     Timer Set  dev=0  chan=0  ticks=${ticks}
    API Call Should Succeed     PHiLIP.Read Trace
    ${trace}=                   API Result Data As List
    Length Should Be Greater    ${trace}  1
    ${delay}=                   Evaluate  ${trace}[-1][time] - ${trace}[-2][time]
    Log Many                    ${ticks}  ${delay}
